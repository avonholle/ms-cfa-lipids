[[init]]
iterators = agecut group sex;

agecut = 2:4;
group = 1:3;
sex = 1;

bmisd#agecut = 6.0 6.9 6.3;
agesd#agecut = 2.3 8.6 8.2;

agecutnames#agecut = "12to19" "20to50" "50to80";
groupnames#group = "year" "race" "sex";

datanames#sex = "totdatlong.dat";
sexnames#sex = "all";

filename = "m1-agegrp-[[agecutnames#agecut]]-groups-[[groupnames#group]]-sex-[[sexnames#sex]]-CFA.inp";

outputDirectory = "C:/temp/models2";
[[/init]]

TITLE: complex cfa nhanes lipids adjust for age, meds and gender -- 
     different intercepts for each year group but same factor loadings
     modl 1: metric invariance. age: [[agecutnames#agecut]],
     group: [[groupnames#group]], sex: [[sexnames#sex]];
     
DATA: FILE = [[datanames#sex]];
VARIABLE: 

NAMES = seqn tg ldl hdl ridreth1 age gender 
wtmec2yr sdmvpsu sdmvstra chol_med year race
rn id white tg2 ldl2 hdl2 tg3 ldl3 hdl3
lbxtr lbdldl lbdhdd age_cut mec10yr bmi male;
MISSING=.;

[[group=1]]
USEVARIABLES ARE tg ldl hdl age bmi;
GROUPING is year (  2003 = y2003
                    2005 = y2005 2007 = y2007 2009 = y2009
                    2011 = y2011); ! See example 5.27
[[/group=1]]                    

[[group=2]]
USEVARIABLES ARE tg ldl hdl age bmi;
GROUPING is race (  1 = MA 2 = OH 3 = NHW 4 = NHB 5 = OTH); ! See example 5.27
[[/group=2]]

[[group=3]]
USEVARIABLES ARE tg ldl hdl age bmi;
GROUPING is male (1 = Male 0 = Female); ! See example 5.27
[[/group=3]]

useobservations = age_cut == [[agecut]] and chol_med==0 and year>2001;
stratification = sdmvstra;
cluster = seqn;
weight = mec10yr;

analysis: 
type = complex;
iterations = 5000;

model:
    f1 by tg* hdl ldl;
    f1@1; [f1@0];
    
    !tg@0.6 ! have to fix tg or get negative resid var. skew?
    ldl with hdl (cov1);
    !tg with hdl (cov1); # note: not sig with age and bmi;
    
    age@4; [age@15]; ! set residual variance to simplify model;
    agef by age;
    bmi@30; [bmi@24];
    bmif by bmi;
    
    bmif with agef (cov2);
    agef with f1 (cov3);
    bmif with f1 (cov4);
    
    tg (tgvar);
  
  model constraint:
      0 < tgvar;   
!      cov1 = 0.1;   
!      cov2 = 3;
!      cov3 = 0.4;
!      cov4 = 3.5;    
      
[[group=1]]
  model y2003:
      [tg];
      [hdl];
      [ldl];
  model y2005:
      [tg];
      [hdl];
      [ldl];
  model y2007:
      [tg];
      [hdl];
      [ldl];
  model y2009:
      [tg];
      [hdl];
      [ldl];
  model y2011:
      [tg];
      [hdl];
      [ldl];
[[/group=1]]
    
[[group=2]]

  model OH:
      [tg];
      [hdl];
      [ldl];
  model NHW:
      [tg];
      [hdl];
      [ldl];
  model NHB:
      [tg];
      [hdl];
      [ldl];
  model OTH:
      [tg];
      [hdl];
      [ldl];
[[/group=2]]

[[group=3]]
  model Male:
      [tg];
      [hdl];
      [ldl];
  model Female:
      [tg];
      [hdl];
      [ldl];
[[/group=3]]


OUTPUT: 
    standardized sampstat;